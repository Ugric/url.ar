let decode(data) = do
    data = data.replace("+", " ")
    let split = data.split("%")
    if (split.length == 1) return data
    let decoded = buffer().from(split[0])
    for (i from 1 to split.length) do
        if (split[i].length < 2) do
            decoded.append("%"+split[i])
            continue
        let hex = split[i][0:2]
        decoded.append(number("0x"+hex))
        decoded.append(split[i][2:])
    if (decoded.length == 0) return data
    return decoded.to("string")

let toNotEncode = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789-_.~ "

let encode(data) = do
    let encoded = []
    let i = 0
    while (i < data.length) do
        if (data[i] in toNotEncode) do
            encoded.append(data[i])
        else do
            encoded.append("%"+hex(ord(data[i])))
        i = i + 1
    return encoded.join("").replace(" ", "+")

let decodeURLQuery(querytext) = do
    let query = {}
    let querySplit = querytext.split("&")
    for (i from 0 to querySplit.length) do
        let queryItem = querySplit[i].splitN("=", 2)
        let nameDecoded = decode(queryItem[0])
        if (queryItem.length != 2) query[nameDecoded] = ""
        else query[nameDecoded] = decode(queryItem[1])
    
    return query

let split(url) = do
    let split = url.splitN("?", 2)
    if (split.length != 2) return {path: split[0], query: {}}
    else return {path: split[0], query: decodeURLQuery(split[1])}

let queryStr(query) = do
    let queryStr = []
    let keys = query.keys()
    for (i from 0 to keys.length) do
        queryStr.append(encode(keys[i])+"="+encode(query[keys[i]]))
    return queryStr.join("&")


let URL(url) = do
    let protocol = null
    let host = null
    let port = null
    let path = null
    let query = null
    let hash = null
    let splitFunc = split
    try do
        let split = url.split("://")
        if (split.length == 1) do
            split = url.split("/")
            if (split.length == 1) do
                host = split[0]
                path = "/"
            else do
                host = split[0]
                path = "/"+split[1:].join("/")
        else do
            protocol = split[0]
            split = split[1].split("/")
            if (split.length == 1) do
                host = split[0]
                path = "/"
            else do
                host = split[0]
                path = "/"+split[1:].join("/")
        split = host.split(":")
        if (split.length == 1) do
            host = split[0]
            if (protocol == "http") do
                port = 80
            else if (protocol == "https") do
                port = 443
        else do
            host = split[0]
            port = number(split[1:].join(":"))
        split = path.split("#")
        if (split.length == 1) do
            path = split[0]
        else do
            path = split[0]
            hash = split[1:].join("#")
        let split = splitFunc(path)
        path = split.path
        query = split.query
    catch (e) do
    
    return {protocol: protocol, host: host, port: port, path: path, query: query, hash: hash}

if (program.main) do
    term.log(URL("7y86tyguhiu7yguhu"))
